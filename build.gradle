plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2025.2.1"
    id "com.google.protobuf" version "0.9.3"
    id "groovy" // Required for ConfigSlurper
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

def ROBOT_MAIN_CLASS = "frc.robot.Main"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project DeployUtils.
deploy {
    targets {
        roborio(getTargetTypeClass('RoboRIO')) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = project.frc.getTeamNumber()
            debug = project.frc.getDebugOrDefault(false)

            artifacts {
                // First part is artifact name, 2nd is artifact type
                // getTargetTypeClass is a shortcut to get the class type using a string

                frcJava(getArtifactTypeClass('FRCJavaArtifact')) {
                }

                // Static files artifact
                frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
                    files = project.fileTree('src/main/deploy')
                    directory = '/home/lvuser/deploy'
                    deleteOldFiles = false // Change to true to delete files on roboRIO that no
                                           // longer exist in deploy directory of this project
                }
            }
        }
    }
}

def deployArtifact = deploy.targets.roborio.artifacts.frcJava

// Set to true to use debug for JNI.
wpi.java.debugJni = false

// Set this to true to enable desktop support.
def includeDesktopSupport = false

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 5.
dependencies {
    annotationProcessor wpi.java.deps.wpilibAnnotations()
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    roborioDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.roborio)
    roborioDebug wpi.java.vendor.jniDebug(wpi.platforms.roborio)

    roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
    roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "com.google.protobuf:protobuf-java:3.22.2"
    implementation 'org.ejml:ejml-all:0.41'
    implementation 'org.java-websocket:Java-WebSocket:1.5.3'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    
    implementation 'org.photonvision:photonlib-java:v2025.1.1'
    implementation 'org.photonvision:photontargeting-java:v2025.1.1'
    
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation "com.google.protobuf:protobuf-java:3.22.2"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// buildDynamically("./build-config.ini")
// generateJsonConfig()

test {
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

// Simulation configuration (e.g. environment variables).
wpi.sim.addGui().defaultEnabled = true
wpi.sim.addDriverstation()

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    from sourceSets.main.allSource
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// Configure jar and deploy tasks
deployArtifact.jarTask = jar
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

// Configure string concat to always inline compile
tasks.withType(JavaCompile) {
    options.compilerArgs.add '-XDstringConcat=inline'
}

def generateJsonConfig() {
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()

    try {
        exec {
            commandLine "npm", "run", "config", "--silent", "--", "--dir", "src/config"
            standardOutput = stdout
            errorOutput = stderr
            ignoreExitValue = false
        }

        def deployDir = new File("src/main/deploy")
        deployDir.mkdirs()
        new File(deployDir, "config").text = stdout.toString().trim()
    } catch (Exception e) {
        println "Failed to generate config: ${e.message}"
        println "Error Output:\n${stderr.toString().trim()}"
    }
}

def buildDynamically(configFilePath) {
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    def buildFailed = false

    try {
        exec {
            commandLine "python3", "scripts/clone_and_build_repos.py", "--config-file-path", configFilePath
            standardOutput = stdout
            errorOutput = stderr
            ignoreExitValue = false
        }
    } catch (Exception e) {
        buildFailed = true
        // ANSI escape code for red: \u001B[31m, reset: \u001B[0m
        println "\n\n" +
                "\u001B[31m" +
                "#############################################################\n" +
                "#                                                           #\n" +
                "#                FAILED TO BUILD DEPENDENCIES!              #\n" +
                "#                                                           #\n" +
                "#############################################################\n" +
                "\u001B[0m"
        println "\u001B[31mExecution failed: ${e.message}\u001B[0m"
    }

    if (stdout.toString().contains("FAILED") || stderr.toString().toLowerCase().contains("error") || buildFailed) {
        println "\n\n" +
                "\u001B[31m" +
                "#############################################################\n" +
                "#                                                           #\n" +
                "#                FAILED TO BUILD DEPENDENCIES!              #\n" +
                "#                                                           #\n" +
                "#############################################################\n" +
                "\u001B[0m"
        println "\u001B[31mError Output:\n${stderr.toString().trim()}\u001B[0m"
        throw new Exception("Failed to build dependencies")
    }

    println "Standard Output:\n${stdout.toString().trim()}"
    println "Error Output:\n${stderr.toString().trim()}"

    // ANSI escape code for green: \u001B[32m, reset: \u001B[0m
    println "\n\n" +
        "\u001B[32m" +
        "#############################################################\n" +
        "#                                                           #\n" +
        "#                BUILT DYNAMIC DEPENDENCIES!                #\n" +
        "#                                                           #\n" +
        "#############################################################\n" +
        "\u001B[0m"

    def jarFiles = fileTree(dir: "lib/build", include: '*.jar')

    project.afterEvaluate {
        dependencies {
            implementation files(jarFiles)
        }
    }
}

buildDynamically("./build-config.ini")
generateJsonConfig()

tasks.register("generateConfig") {
    group = "build"
    description = "Generate config.json from TSX output"
    doLast {
        generateJsonConfig()
    }
}


tasks.build.dependsOn(tasks.generateConfig)
tasks.build.dependsOn(tasks.generateProto)

tasks.jar.dependsOn(tasks.generateProto)
tasks.compileJava.dependsOn(tasks.generateProto)

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.22.2" // Use the desired protoc version
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { // Enable Java generation
                    option 'lite' // Use this option for smaller code (optional)
                }
            }
        }
    }
    // Specify the proto source directory
    generatedFilesBaseDir = "$buildDir/generated/source"
}

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated/source/proto/main/java"
        }
        proto {
            srcDirs = ['src/proto']
        }
    }
}